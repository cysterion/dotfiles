#!/usr/bin/env zsh
autoload -U colors
colors

# Show different symbols as appropriate for various Git repository states
GIT_WHERE="$(git symbolic-ref -q HEAD 2> /dev/null \
    || git name-rev --name-only --no-undefined --always HEAD 2> /dev/null)"
if [[ -n "$GIT_WHERE" ]]; then
    is_zsh=0
    offset=0
    padleft=0
    padright=0
    while [ $# -ne 0 ]
    do
        case "$1" in
            '--zsh')
                is_zsh=1
                ;;
            '-s')
                shift
                offset=$1
                ;;
            '-l')
                padleft=1
                ;;
            '-r')
                padright=1
                ;;
        esac
        shift
    done
    # Modify the colors and symbols in these variables as desired.
    GIT_PROMPT_OPEN="%{$fg_bold[yellow]%}[%{$reset_color%}"
    GIT_PROMPT_SYMBOL="\u2387 "
    GIT_PROMPT_AHEAD="\u2191NUM"
    GIT_PROMPT_BEHIND="\u2193NUM"
    GIT_PROMPT_MERGING="\u2211"
    GIT_PROMPT_COLOR="%{$fg[cyan]%}"
    GIT_PROMPT_AHEAD_COLOR="%{$fg[green]%}"
    GIT_PROMPT_BEHIND_COLOR="%{$fg[yellow]%}"
    GIT_PROMPT_MERGING_COLOR="%{$fg[magenta]%}"
    GIT_PROMPT_MODIFIED="%{$fg_bold[red]%}\u2717%{$reset_color%}"
    GIT_PROMPT_STAGED="%{$fg_bold[green]%}\u2713%{$reset_color%}"
    GIT_PROMPT_UNTRACKED="%{$fg_bold[yellow]%}\u2026%{$reset_color%}"
    GIT_PROMPT_CLOSE="%{$fg_bold[yellow]%}]%{$reset_color%}"
    if [ $offset -gt 0 ]
    then
        GIT_PROMPT_SYMBOL="$GIT_PROMPT_SYMBOL "
    fi

    GIT_COLOR="$GIT_PROMPT_COLOR"
    # Compose this value via multiple conditional appends.
    GIT_STATE=""

    NUM_AHEAD="$(git log --oneline @{u}.. 2> /dev/null\
        | wc -l | tr -d ' ')"
    if [ "$NUM_AHEAD" -gt 0 ]; then
        GIT_COLOR="$GIT_PROMPT_AHEAD_COLOR"
        GIT_STATE="$GIT_STATE${GIT_PROMPT_AHEAD//NUM/$NUM_AHEAD}"
    fi

    NUM_BEHIND="$(git log --oneline ..@{u} 2> /dev/null\
        | wc -l | tr -d ' ')"
    if [ "$NUM_BEHIND" -gt 0 ]; then
        GIT_COLOR="$GIT_PROMPT_BEHIND_COLOR"
        GIT_STATE="$GIT_STATE${GIT_PROMPT_BEHIND//NUM/$NUM_BEHIND}"
    fi

    GIT_DIR="$(git rev-parse --git-dir 2> /dev/null)"
    if [ -n $GIT_DIR ] && test -r $GIT_DIR/MERGE_HEAD; then
        GIT_COLOR="$GIT_PROMPT_MERGING_COLOR"
        GIT_STATE="$GIT_STATE$GIT_PROMPT_MERGING"
    fi

    if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
        GIT_STATE="$GIT_STATE$GIT_PROMPT_UNTRACKED"
    fi

    if ! git diff --quiet 2> /dev/null; then
        GIT_STATE="$GIT_STATE$GIT_PROMPT_MODIFIED"
    fi

    if ! git diff --cached --quiet 2> /dev/null; then
        GIT_STATE="$GIT_STATE$GIT_PROMPT_STAGED"
    fi

    # Compose the final git status line
    GIT_STRING="$GIT_PROMPT_OPEN$GIT_COLOR$GIT_PROMPT_SYMBOL${GIT_WHERE#(refs/heads/|tags/)}%{$reset_color%}$GIT_PROMPT_CLOSE"
    if [[ -n $GIT_STATE ]]
    then
        GIT_STRING="$GIT_PROMPT_OPEN$GIT_STATE$GIT_PROMPT_CLOSE$GIT_STRING"
    fi
    if [ $padleft -eq 1 ]
    then
        GIT_STRING=" $GIT_STRING"
    fi
    if [ $padright -eq 1 ]
    then
        GIT_STRING="$GIT_STRING "
    fi

    if [ $is_zsh -eq 1 ]; then
        echo "$GIT_STRING"
    else
        echo "$GIT_STRING" | sed -e s/%}//g -e s/%{//g
    fi
fi

